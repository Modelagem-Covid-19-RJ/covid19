{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using StatsBase, Plotly, BenchmarkTools"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "struct Bairro\n",
    "    nResidencias::Integer # numero de residencias naquele bairro\n",
    "    nPessoas::Integer # numero de pessoas naquele bairro\n",
    "    residencias::Array{T} where T <: Integer # indice das residencias do bairro\n",
    "    pessoas::Array{T} where T <: Integer # indice das pessoas do bairro\n",
    "    distancias::Array{T} where T <: Number # distancia media entre uma pessoa do bairro e os demais bairros\n",
    "end\n",
    "\n",
    "struct Residencia\n",
    "    n::Integer # numero de pessoas naquela residencia\n",
    "    pessoas::Array{T} where T <: Integer # indice das pessoas da residencia\n",
    "    bairro::Integer # indice do bairro que a residencia pertence\n",
    "    posicao::Array{T} where T <: Number # posicao da residencia no mapa\n",
    "end\n",
    "\n",
    "struct Social\n",
    "    n::Integer # numero de pessoas naquela rede social\n",
    "    pessoas::Array{T} where T <: Integer # pessoas que coimpoe a rede social\n",
    "end\n",
    "\n",
    "struct Pessoas\n",
    "    residencias::Array{T} where T <: Integer # lista com indices da residencia onde cada uma das pessoas residem\n",
    "    bairros::Array{T} where T <: Integer # lista com indices do bairro onde cada uma das pessoas residem\n",
    "    social::Array{T} where T <: Integer # lista com indices da rede social na qual cada uma das pessoas pertencem\n",
    "    posicao::Array{T, 2} where T <: Number # posicao de cada pessoa\n",
    "end\n",
    "\n",
    "struct Populacao\n",
    "    rodada::Integer # n da rodada (nome da pasta com arquivos)\n",
    "    n::Integer # tamanho da população\n",
    "    I0::Integer # quantidade inicial de infectados\n",
    "    estadoInicial::Array{T} where T <: Integer # estado inicial para cada individuo\n",
    "    estadoAtual::Array{T} where T <: Integer # estado inicial de cada individuo\n",
    "    pessoas::Pessoas # pessoas que compoem a populacao\n",
    "    residencias::Array{Residencia} # lista com as residencias\n",
    "    bairros::Array{Bairro} # lista de bairros\n",
    "    sociais::Array{Social} # lista de redes sociais\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function selectiveRand(v)\n",
    "    \"\"\"\n",
    "        Função para geração de números aleatórios de acordo com um vetor booleano\n",
    "    \n",
    "        Parametros:\n",
    "            v: vetor booleano\n",
    "        \n",
    "        Saida:\n",
    "            aux: vetor com um número aleatório nas entradas verdadeiras de v o 0 nas demais\n",
    "    \"\"\"\n",
    "    aux = zeros(length(v))\n",
    "    aux[v] .= rand(sum(v))\n",
    "    return aux\n",
    "end\n",
    "\n",
    "function rowWiseNorm(A)\n",
    "    \"\"\"\n",
    "        Função para calcular a norma dos vetores linha de uma matriz\n",
    "    \"\"\"\n",
    "    return sqrt.(sum(abs2, A, dims=2)[:, 1])\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function calculaDistanciaFina(populacao::Populacao, bairro::Bairro, suscetiveisBairro, infectadosBairro, fKernel; T=Float16)\n",
    "    \"\"\"\n",
    "        Cálculo da distancia entre as pessoas de um mesmo bairro\n",
    "    \"\"\"\n",
    "    aux = zeros(T, length(suscetiveisBairro), length(infectadosBairro))\n",
    "    Threads.@threads for i in 1:length(suscetiveisBairro)\n",
    "        aux[i, :] += fKernel(populacao.pessoas.posicao[infectadosBairro] .- populacao.pessoas.posicao[suscetiveisBairro[i], :]')\n",
    "    end\n",
    "    return aux\n",
    "end\n",
    "\n",
    "function calculaDistancia(populacao::Populacao, suscetiveis, infectados, fKernel)\n",
    "    \"\"\"\n",
    "        Calculo da distância entre todas as pessoas, tomando a distnacia media entre pessoas de bairros diferentes\n",
    "    \"\"\"\n",
    "    aux = zeros(populacao.n)\n",
    "    infectadosBairros = [sum(infectados[i.pessoas]) for i in populacao.bairros]\n",
    "    for bairro in populacao.bairros\n",
    "        suscetiveisBairro = bairro.pessoas[suscetiveis[bairro.pessoas]]\n",
    "        infectadosBairro = bairro.pessoas[infectados[bairro.pessoas]]\n",
    "        aux[suscetiveisBairro] .+= ones(length(suscetiveisBairro)) .* sum(infectadosBairros .* bairro.distancias)\n",
    "        aux[suscetiveisBairro] .+= sum(calculaDistanciaFina(populacao, bairro, suscetiveisBairro, infectadosBairro, fKernel), dims=2)[:, 1]\n",
    "    end\n",
    "    return aux[suscetiveis]\n",
    "end\n",
    "\n",
    "function escreveDistancias(populacao, rodada, fKernel; T=Float16)\n",
    "    \"\"\"\n",
    "        Calcula e escreve a matriz de distancias entre pessoas do mesmo bairro, para todos os bairros\n",
    "    \"\"\"\n",
    "    rm(joinpath(\"saidas\", string(rodada)), recursive=true, force=true)\n",
    "    mkdir(joinpath(\"saidas\", string(rodada)))\n",
    "    dir = joinpath(\"saidas\", string(rodada), \"dist\")\n",
    "    mkdir(dir)\n",
    "    for i in 1:length(populacao.bairros)\n",
    "        bairro = populacao.bairros[i]\n",
    "        file = open(joinpath(dir, string(i) * \".eps\"), \"w\")\n",
    "        aux = Array{T, 2}(calculaDistanciaFina(populacao, bairro, bairro.pessoas, bairro.pessoas, fKernel))\n",
    "        write(file, aux)\n",
    "        close(file)\n",
    "        aux = nothing\n",
    "        file = nothing\n",
    "        GC.gc()\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function miniPassoMatricial(estados::Array{T} where T <: Integer)\n",
    "    \"\"\"\n",
    "        Passo matricial para calcular exposicao de uma rede completa\n",
    "    \"\"\"\n",
    "    popSuscetiveis = estados.==1\n",
    "    popInfectados = estados.==2\n",
    "    \n",
    "    n = length(estados)\n",
    "    nInfectados = sum(popInfectados)\n",
    "    nSuscetiveis = sum(popSuscetiveis)\n",
    "\n",
    "    expostos = zeros(Bool, n, n)\n",
    "    expostos[popSuscetiveis, popInfectados] .= true\n",
    "    return sum(expostos, dims=2)[:, 1]\n",
    "end\n",
    "\n",
    "function abreEps(rodada, i, T=Float16)\n",
    "    \"\"\"\n",
    "        Abre a matriz de distancias de um bairro\n",
    "    \"\"\"\n",
    "    file = open(joinpath(\"saidas\", string(rodada), \"dist\", string(i) * \".eps\"))\n",
    "    aux = reinterpret(T, read(file))\n",
    "    close(file)\n",
    "    n = Int(sqrt(length(aux)))\n",
    "    return reshape(aux, (n, n))\n",
    "end\n",
    "\n",
    "function leDistancia(populacao::Populacao, suscetiveis, infectados, fKernel)\n",
    "    \"\"\"\n",
    "        Calcula soma das distancias entre os infectados e os suscetiveis, \n",
    "        tomando a distancia media entre pessoas de bairros diferentes e lendo a matriz de distancias\n",
    "        para pessoas do mesmo bairro.\n",
    "    \"\"\"\n",
    "    aux = zeros(populacao.n)\n",
    "    infectadosBairros = [sum(infectados[i.pessoas]) for i in populacao.bairros]\n",
    "    Threads.@threads for i in 1:length(populacao.bairros)\n",
    "        bairro = populacao.bairros[i]\n",
    "        suscetiveisBairro = suscetiveis[bairro.pessoas]\n",
    "        infectadosBairro = infectados[bairro.pessoas]\n",
    "        aux[bairro.pessoas[suscetiveisBairro]] .+= ones(sum(suscetiveisBairro)) .* sum(infectadosBairros .* bairro.distancias)\n",
    "        aux[bairro.pessoas[suscetiveisBairro]] .+= sum(abreEps(populacao.rodada, i)[suscetiveisBairro, infectadosBairro], dims=2)[:, 1]\n",
    "    end\n",
    "    return aux[suscetiveis]\n",
    "end\n",
    "\n",
    "function passoMisto(populacao::Populacao, \n",
    "        α::Array{T} where T <: Number, β::Array{T} where T <: Number, θ::Array{T} where T <: Number, \n",
    "        γ::Number, δ::Number, fKernel)\n",
    "    \"\"\"\n",
    "        Entrada:\n",
    "            populacao: \n",
    "            α: taxa de transmissão residencial\n",
    "            β: taxa de transmissão social\n",
    "            θ: taxa de transmissão global\n",
    "            γ: probabilidade de não recuperação\n",
    "            δ: tamanho do passo temporal\n",
    "            fKernel: ?\n",
    "    \"\"\"\n",
    "    popSuscetiveis = populacao.estadoAtual.==1\n",
    "    popInfectados = populacao.estadoAtual.==2\n",
    "    \n",
    "    contatos = zeros(populacao.n)\n",
    "    Threads.@threads for i in populacao.residencias\n",
    "        contatos[i.pessoas] .+= miniPassoMatricial(populacao.estadoAtual[i.pessoas]) .* α[i.pessoas]\n",
    "    end\n",
    "    \n",
    "    Threads.@threads for i in populacao.sociais\n",
    "        contatos[i.pessoas] .+= miniPassoMatricial(populacao.estadoAtual[i.pessoas]) .* β[i.pessoas]\n",
    "    end\n",
    "    \n",
    "    contatos[popSuscetiveis] .+= leDistancia(populacao, popSuscetiveis, popInfectados, fKernel) .* θ[popSuscetiveis]\n",
    "    \n",
    "    prob = exp.(- δ .* contatos)\n",
    "    \n",
    "    novosInfectados = selectiveRand(popSuscetiveis) .> prob\n",
    "    novosRecuperados = selectiveRand(popInfectados) .> γ\n",
    "    \n",
    "    infectados = ((popInfectados .& (.~novosRecuperados)) .| novosInfectados)\n",
    "    suscetiveis = (popSuscetiveis .& (.~novosInfectados))\n",
    "\n",
    "    return 3 .* ones(Int, populacao.n) .- 2 .* suscetiveis .- infectados\n",
    "end\n",
    "\n",
    "function evolucaoMista(\n",
    "        populacao::Populacao, tempos::AbstractArray{T} where T <: Number, nSim::Integer, \n",
    "        α::Array{T} where T <: Number, β::Array{T} where T <: Number, θ::Array{T} where T <: Number, γ::Number, fKernel)\n",
    "    \"\"\"\n",
    "        Entrada:\n",
    "            resumoPop: \n",
    "            tempos:\n",
    "            α: taxa de transmissão residencial\n",
    "            β: taxa de transmissão social\n",
    "            θ: taxa de transmissão global\n",
    "            γ: parâmetro da exponencial de não recuperação\n",
    "            δ: tamanho do passo temporal\n",
    "            fKernel: ?\n",
    "    \"\"\"    \n",
    "    nT = length(tempos)\n",
    "    passos = tempos[2:end] - tempos[1:(end-1)]\n",
    "    Γ = exp.(- γ .* passos)\n",
    "    \n",
    "    S = zeros(nSim, nT)\n",
    "    I = zeros(nSim, nT)\n",
    "    R = zeros(nSim, nT)\n",
    "    \n",
    "    S[:, 1] .= populacao.n - populacao.I0\n",
    "    I[:, 1] .= populacao.I0\n",
    "    \n",
    "    for j in 1:nSim\n",
    "        populacao.estadoAtual .= populacao.estadoInicial\n",
    "        for (k, δ) in enumerate(passos)\n",
    "            @time populacao.estadoAtual .= passoMisto(populacao, α, β, θ, Γ[k], δ, fKernel)\n",
    "            GC.gc()\n",
    "            \n",
    "            S[j, k+1] = sum(populacao.estadoAtual .== 1)\n",
    "            I[j, k+1] = sum(populacao.estadoAtual .== 2)\n",
    "            R[j, k+1] = sum(populacao.estadoAtual .== 3)\n",
    "        end\n",
    "    end\n",
    "    return S,I,R\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function geraPopulacaoAleatoria(rodada, n, I0, nResidencias, nSociais, bairrosShape, fKernel)\n",
    "    \"\"\"\n",
    "        Gera populacao aleatoria de maneira simplificada\n",
    "    \"\"\"\n",
    "    nBairros = prod(bairrosShape)\n",
    "    pop0 = ones(Int, n)\n",
    "    \n",
    "    # escolhe pessoas aleatorias para comecarem infectadas\n",
    "    pop0[StatsBase.sample(1:n, I0, replace=false)] .= 2;\n",
    "\n",
    "    # distribui as pessoas nas residencias\n",
    "    residencia = rand(1:nResidencias, n)\n",
    "    \n",
    "    # determina que pessoas estão em cada residencia\n",
    "    residencias = [Int[] for i in 1:nResidencias]   \n",
    "    Threads.@threads for i in 1:nResidencias\n",
    "        append!(residencias[i], (1:n)[residencia .== i])\n",
    "    end\n",
    "    \n",
    "    # atribui uma posicao e determina o bairro que a residencia pertence\n",
    "    residenciasPos = rand(nResidencias, 2) .* bairrosShape'\n",
    "    residenciasBai = [\n",
    "        ceil(Int, residenciasPos[i, 1]) + \n",
    "        floor(Int, residenciasPos[i, 2]) * bairrosShape[1]\n",
    "        for i in 1:nResidencias\n",
    "    ]\n",
    "    \n",
    "    # cria listas com residencias\n",
    "    residencias = [Residencia(length(j), j, residenciasBai[i], residenciasPos[i, :]) for (i, j) in enumerate(residencias)]\n",
    "    \n",
    "    # cria redes sociais\n",
    "    social = rand(1:nSociais, n)\n",
    "    sociais = [Social(sum(social .== i), (1:n)[social .== i]) for i in 1:nSociais]\n",
    "    \n",
    "    # determina a posicao e bairro de cada pessoa\n",
    "    posicao = zeros(n, 2)\n",
    "    bairro = zeros(Int, n)\n",
    "    Threads.@threads for i in 1:n\n",
    "        posicao[i, :] .= residenciasPos[residencia[i], :]\n",
    "        bairro[i] = residenciasBai[residencia[i]]\n",
    "    end\n",
    "    \n",
    "    pessoas = Pessoas(residencia, bairro, social, posicao)\n",
    "    \n",
    "    # identifica pessoas e residencias de cada bairro\n",
    "    bairrosResidencias = [(1:nResidencias)[residenciasBai .== i] for i in 1:nBairros]\n",
    "    bairrosPessoas = [(1:n)[bairro .== i] for i in 1:nBairros]\n",
    "    \n",
    "    # calcula o centro do bairro e a distancia media entre os bairros\n",
    "    centros = vcat([mean(posicao[i, :], dims=1) for i in bairrosPessoas]...)\n",
    "    dist = hcat([fKernel(centros .- centros[i, :]') for i in 1:nBairros]...)\n",
    "    replace!(dist, NaN=>0.)\n",
    "    \n",
    "    #cria lista de bairros e populacao\n",
    "    bairros = [\n",
    "        Bairro(\n",
    "            length(bairrosResidencias[i]),\n",
    "            length(bairrosPessoas[i]),\n",
    "            bairrosResidencias[i],\n",
    "            bairrosPessoas[i],\n",
    "            dist[i, :]\n",
    "            ) for i in 1:nBairros\n",
    "    ]\n",
    "    populacao = Populacao(rodada, n, I0, pop0, copy(pop0), pessoas, residencias, bairros, sociais)\n",
    "    \n",
    "    # escreve matriz de distancias das pessoas de cada bairro\n",
    "    escreveDistancias(populacao, rodada, fKernel)\n",
    "    return populacao\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function powerDecay(a::Array)\n",
    "    b = rowWiseNorm(a)\n",
    "    return b ./ (b .+ 1.5);\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function foo(k, bairros)\n",
    "    rodada = length(readdir(\"saidas\")) + 1\n",
    "    k = floor(Int, k / 100)\n",
    "\n",
    "    n = 100 * k\n",
    "    I0 = 1 * k\n",
    "    nResidencias = 30 * k\n",
    "    nSociais = 1 * k\n",
    "    return geraPopulacaoAleatoria(rodada, n, I0, nResidencias, nSociais, bairros, powerDecay);\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "bairrosShape = [5, 2]\n",
    "@time populacao = foo(1e+4, bairrosShape);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "data = Dict([\n",
    "    \"x\" => populacao.pessoas.posicao[:, 1],\n",
    "    \"autobinx\" => false,\n",
    "    \"xbins\" => Dict([\n",
    "        \"start\" => 0,\n",
    "        \"end\" => bairrosShape[1],\n",
    "        \"size\" => 1\n",
    "    ]),\n",
    "    \"y\"=>populacao.pessoas.posicao[:, 2],\n",
    "    \"autobiny\" => false,\n",
    "    \"ybins\" => Dict([\n",
    "        \"start\" => 0,\n",
    "        \"end\" => bairrosShape[2],\n",
    "        \"size\" => 1\n",
    "    ]),\n",
    "]);\n",
    "\n",
    "Plotly.plot(Plotly.histogram2d(data))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "data = Dict([\n",
    "    \"x\"=>populacao.pessoas.posicao[:, 1],\n",
    "    \"y\"=>populacao.pessoas.posicao[:, 2]\n",
    "]);\n",
    "\n",
    "Plotly.plot(Plotly.histogram2dcontour(data))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# tem que rever esses parâmetros\n",
    "α = 1e-1 * ones(populacao.n)\n",
    "β = 1e-4 * ones(populacao.n)\n",
    "@time θ = 1e-1 ./ leDistancia(populacao, ones(Bool, populacao.n), ones(Bool, populacao.n), powerDecay);\n",
    "GC.gc()\n",
    "γ = 0.2;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@time a = evolucaoMista(populacao, Array(1:10), 1, α, β, θ, γ, powerDecay)"
   ]
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": "ade44cf82927411c830147a3c8e885e4",
   "lastKernelId": "418d4519-50f3-4967-b3fe-2b1345c5be4d"
  },
  "kernelspec": {
   "display_name": "Julia 1.4.1",
   "language": "julia",
   "name": "julia-1.4"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.4.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
